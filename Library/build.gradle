apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 235
        versionName "2.3.5"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.example.myapp.MyEventBusIndex']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testApi 'junit:junit:4.12'
    androidTestApi 'com.android.support.test:runner:1.0.1'
    androidTestApi 'com.android.support.test.espresso:espresso-core:3.0.1'

    api 'com.android.support:design:27.0.2'
    api 'com.android.support:appcompat-v7:27.0.2'

    //AndroidUtilCode
    api 'com.blankj:utilcode:1.11.1'
    //PgyerSDK
    api 'com.pgyersdk:sdk:2.8.1'
    //MobStat
    api 'com.baidu.mobstat:mtj-sdk:3.8.1.1'
    //EventBus
    api 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    //DBFlow
    api 'com.github.Raizlabs.DBFlow:dbflow:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-core:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-sqlcipher:4.2.4'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4'
    //Retrofit
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    //Dagger2
    api 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    //RXJava
    api 'io.reactivex.rxjava2:rxjava:2.1.8'
    //RXAndroid
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //RxLifeCycle2
    api 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    api 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'
    //BGASwipeBackLayout
    api 'cn.bingoogolapple:bga-swipebacklayout:1.1.8@aar'
    //Fragmentation
    api 'me.yokeyword:fragmentation:1.2.4'
    //HandyAdapter
    api 'com.github.Handy045:HandyAdapter:1.0.1'
    //BaseRecyclerViewAdapterHelper
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    //Glide
    api 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'
    //ButterKnife
    api 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}

//-------------------- jitPack生成文档 --------------------
tasks.withType(JavaCompile) { //指定编码
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) { //打包源码
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) { //制作文档(Javadoc)
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
