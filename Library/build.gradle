//noinspection GradleDependency
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 250
        versionName "2.5.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.example.myapp.MyEventBusIndex']
            }
        }

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    //androidSupport
    api 'com.android.support:design:28.0.0'
    api 'com.android.support:appcompat-v7:28.0.0'
    //gson
    api 'com.google.code.gson:gson:2.8.5'
    //fastjson
    api 'com.alibaba:fastjson:1.2.54'
    //AndroidUtilCode
    api 'com.blankj:utilcode:1.22.10'
    //Bugly
    api 'com.tencent.bugly:crashreport:2.8.6.0'
    api 'com.tencent.bugly:nativecrashreport:3.6.0.1'
    //EventBus
    api 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    //DBFlow
    api 'com.github.Raizlabs.DBFlow:dbflow:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-core:4.2.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-sqlcipher:4.2.4'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4'
    //Retrofit
    api 'com.squareup.retrofit2:retrofit:2.5.0'
    //Dagger2
    api 'com.google.dagger:dagger:2.20'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.20'
    //RXJava
    api 'io.reactivex.rxjava2:rxjava:2.2.5'
    //RXAndroid
    api 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //RxLifeCycle2
    api 'com.trello.rxlifecycle2:rxlifecycle:2.2.2'
    api 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    //BGASwipeBackLayout
    api 'cn.bingoogolapple:bga-swipebacklayout:1.2.0'
    //BaseRecyclerViewAdapterHelper
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.44'
    //Glide
    api 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    //ButterKnife
    api 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}

//-------------------- jitPack生成文档 --------------------
tasks.withType(JavaCompile) { //指定编码
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) { //打包源码
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) { //制作文档(Javadoc)
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
